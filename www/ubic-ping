#!/usr/bin/perl
# Copyright (c) 2009 Yandex.ru

package Ubic::Ping;

use strict;
use warnings;

=head1 NAME

ubic-ping - http server which returns service status by it's name or port

=cut

use Ubic::Catalog;

use base qw(HTTP::Server::Simple::CGI);

sub handle_request {
    my ($self, $cgi) = @_;
    my $service;
    if (my $port = $cgi->param('port')) {
        # find service my port
        my @services = Ubic::Catalog->services();
        for (@services) {
            if ($_->port and $_->port == $port) {
                $service = $_;
                last;
            }
        }
        unless ($service) {
            print "HTTP/1.0 404 Not found\r\n\r\n";
            print "Service at port $port not found\n";
            return;
        }
    }
    elsif (my $name = $cgi->param('name')) {
        eval {
            $service = Ubic::Catalog->service($name);
        }; if ($@) {
            print "HTTP/1.0 404 Not found\r\n\r\n";
            print "Service with name $name not found\n";
            return;
        }
    }
    else {
        print "HTTP/1.0 404 Not found\r\n\r\n";
        print "Expected name or port\n";
        return;
    }
    if ($service->is_enabled) {
        print "HTTP/1.0 200 OK\r\n\r\n";
        print "ok\n";
        return;
    }
    else {
        print "HTTP/1.0 200 OK\r\n\r\n";
        print "disabled\n";
        return;
    }
}

1;

=head1 AUTHOR

Vyacheslav Matjukhin <mmcleric@yandex-team.ru>

=cut
